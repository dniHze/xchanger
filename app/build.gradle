apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "dev.dnihze.revorate"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "0.1.6"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            storeFile file(System.getenv("KEYSTORE") ?: "../keystore.jks")
            storePassword System.getenv("KEYSTORE_PASSWORD")
            keyAlias System.getenv("KEY_ALIAS")
            keyPassword System.getenv("KEY_PASSWORD")
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    lintOptions {
        disable 'ObsoleteLintCustomCheck'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8.toString()
    }
}

kapt {
    arguments {
        arg("dagger.formatGeneratedSource", "disabled")
        arg("dagger.gradle.incremental", "enabled")
        arg("room.schemaLocation", "$projectDir/schemas".toString())
    }
}

dependencies {
    implementation Dependencies.kotlinStdLib

    implementation (Dependencies.appCompat) {
        exclude group: 'androidx.cursoradapter', module: 'cursoradapter'
        exclude group: 'androidx.fragment', module: 'fragment'
        exclude group: 'androidx.drawerlayout', module: 'drawerlayout'
    }
    implementation Dependencies.ktxCore

    implementation Dependencies.emoji
    implementation Dependencies.emojiCompat

    implementation (Dependencies.material) {
        exclude group: 'androidx.fragment', module: 'fragment'
        exclude group: 'androidx.transition', module: 'transition'
        exclude group: 'androidx.viewpager2', module: 'viewpager2'
    }

    implementation Dependencies.recyclerView

    implementation (Dependencies.liveData)  {
        exclude group: 'org.jetbrains.kotlinx', module: 'kotlinx-coroutines-core'
        exclude group: 'org.jetbrains.kotlinx', module: 'kotlinx-coroutines-android'
    }
    implementation (Dependencies.viewModel) {
        exclude group: 'org.jetbrains.kotlinx', module: 'kotlinx-coroutines-core'
        exclude group: 'org.jetbrains.kotlinx', module: 'kotlinx-coroutines-android'
    }

    implementation Dependencies.viewModelSavedState

    implementation Dependencies.lifecycleProcess
    kapt Dependencies.lifecycleCompiler

    implementation Dependencies.insetter
    implementation Dependencies.insetterKTX

    api Dependencies.dagger2
    kapt Dependencies.dagger2Compiler

    implementation (Dependencies.moshiKotlin) {
        exclude group: 'org.jetbrains.kotlin', module: 'kotlin-reflect'
    }
    kapt Dependencies.moshiCodegen

    implementation Dependencies.retrofit
    implementation Dependencies.retrofitRxJavaAdapter
    implementation Dependencies.retrofitMoshiConverter

    implementation Dependencies.rxJava2
    implementation Dependencies.rxAndroid2

    implementation Dependencies.room
    kapt Dependencies.roomCompiler
    implementation Dependencies.roomRxJava2

    implementation Dependencies.rxRedux
    implementation Dependencies.rxRelay

    implementation Dependencies.okHttp

    implementation Dependencies.cicerone
    implementation Dependencies.timber

    debugImplementation Dependencies.stetho
    debugImplementation Dependencies.stehoInterceptor
    debugImplementation Dependencies.leakcanary
    debugImplementation Dependencies.okHttpLoggingInterceptor

    testImplementation TestDependencies.junit
    testImplementation TestDependencies.mockk
    testImplementation TestDependencies.room
    testImplementation TestDependencies.mockOkHttpServer

    androidTestImplementation TestDependencies.androidxJunit
    androidTestImplementation TestDependencies.mockkAndroid
    androidTestImplementation TestDependencies.espressoCore

}
